Use cWebView.pkg
Use cWebPanel.pkg
Use cWebForm.pkg
Use cWebRadio.pkg
Use cWebCheckbox.pkg
Use cWebForm.pkg
Use cWebButton.pkg
Use cWebLabel.pkg
Use cWebGroup.pkg

// Note: This sample does not actually run a DataFlex Reports report. The .pdf file that would normally
// be generated by DataFlex Reports is a previously generated file which was installed with DataFlex.

// If you have an installed version of DataFlex Reports compatible with this version of DataFlex, then
// you could comment out the code below to generate a live DataFlex Reports report.
Object oOrderListSample is a cWebView
    Set psCaption to "Order List (DataFlex Reports)"
    Set piWidth to 700
    Set piColumnCount to 10

// If you are using DataFlex Reports, here is how you would actually
// initialize, run and save the report to a PDF file.

//    Use cDRReport.pkg // Move this line to the other USE statement lines above the cWebView component
//
//    Object oReport is a cDRReport
//        Procedure OnInitializeReport
//            Send SetFilters
//        End_Procedure
//
//        Procedure SetFilters
//            Number nMinOrderTotal
//            String sReportId
//
//            Get psReportId to sReportId
//            Send RemoveAllFilters sReportId  // clear any previous filters
//
//            WebGet psValue of oFromOrderHeaOrderTotal to nMinOrderTotal
//            // Send AddFilter C_USEMAINDRREPORTID "{OrderHea.Order_Total}" C_DRGreaterThanOrEqual nMinOrderTotal
//            // psFilterFunction is displayed in the report's page header so we use this instead of AddFilter
//            Set psFilterFunction C_USEMAINDRREPORTID to ('return ({OrderHea.Order_Total} > ' + String(nMinOrderTotal) + ')')
//        End_Procedure
//
//        Function GenerateReport Returns String
//            String sReportId sPath
//            DRPDFExportOptions PDFReportOptions
//            Boolean bCanceled
//
//            Set psReportName to "OrderList.DR"
//            Get OpenReport to sReportId
//
//            If (sReportId <> "") Begin
//                Get DefaultPDFExportOptions to PDFReportOptions
//                Move C_drHigh to PDFReportOptions.iImageQuality
//                Set pPDFExportOptions to PDFReportOptions
//
//                Get ReportCacheFileName '.pdf' to sPath
//
//                If (sPath <> "") Begin
//                    Send ExportReport C_DRPDF sPath
//
//                    // see if report ran ok, if not return empty path
//                    Get pbCanceled to bCanceled
//                    If (bCanceled) Begin
//                        Move "" to sPath
//                    End
//                End
//
//                Send CloseReport sReportId
//            End
//
//            Function_Return sPath
//        End_Function
//    End_Object

// This is a dummy object that is used in place of a report PDF file that would have been
// generated using DataFlex Reports. Note that this also shows you how to download a
// static file (could be any kind of file) from a secure non-web location. In this case,
// the dummy report is located in \Data, which is not a web path.

    Object oReport is a cObject
        Function GenerateReport Returns String
            String sPath

            Get GetReportsCache of ghoWebApp True to sPath
            If (sPath <> "") Begin
                Move (sPath + "\OrderListSample.pdf") to sPath
            End
            Function_Return sPath
        End_Function
    End_Object

    Procedure RunReport
        String sFile sUrl

        // generate the report file and return its full file path name (not a URL)
        Get GenerateReport of oReport to sFile

        If (sFile="") Begin
            Send ShowInfoBox "The Report could not be generated"
        End
        Else Begin
            Get DownloadURL of ghoWebResourceManager sFile to sUrl

            // this is used to show a PDF file in a new browser tab or window
            // use btNewTab to open a new browser tab page
            // use btNewWindow to open a new browser window
            Send NavigateToPage sUrl btNewTab

            // this is used to show a PDF file in an embedded viewer within this view
            //WebSet psUrl of oWebIFrame1 to sUrl
        End
    End_Procedure

    Object oOrderTotalGroup is a cWebGroup
        Set psCaption to "Minimum Order Total:"
        Set piColumnSpan to 3
        Set piColumnCount to 6

        Object oFromOrderHeaOrderTotal is a cWebForm
            Set psLabel to "From:"
            Set psValue to 5000
            Set piColumnSpan to 6
            Set piLabelOffset to 50
        End_Object
    End_Object

    Object oWebLabel1 is a cWebLabel
        Set piColumnSpan to 0
        Set psCaption to "Note: This view simply displays a static file ('OrderListSample.pdf'), so this setting does not really change the output."
        Set piColumnIndex to 3
    End_Object

    Object oRunButton is a cWebButton
        Set psCaption to "Run Report"
        Set piColumnSpan to 3

        Procedure OnClick
            Send RunReport
        End_Procedure
    End_Object

// this would be used if you wanted to output to an embedded window
//        Object oWebIFrame1 is a cWebIFrame
//           Set pbFillHeight to True
//           Set pbShowBorder to True
//        End_Object
End_Object
