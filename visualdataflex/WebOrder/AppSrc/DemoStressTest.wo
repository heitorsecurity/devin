Use cWebView.pkg
Use cWebForm.pkg
Use cWebButton.pkg
Use cWebGroup.pkg
Use cWebTabContainer.pkg
Use PerformanceTest.pkg

Use Vendor.DD
Use Invt.DD
Use Customer.DD
Use SalesP.DD
Use OrderHea.DD
Use OrderDtl.DD

Object oDemoStressTest is a cWebView
    Set psCaption to "Stress Test"
    Set piColumnCount to 3   
    Set pbFillHeight to True

    Move Self to ghoStressView  

    Object Vendor_DD is a Vendor_DataDictionary
    End_Object    // Vendor_DD

    Object Invt_DD is a Invt_DataDictionary
        Set DDO_Server to Vendor_DD
    End_Object    // Invt_DD

    Object Customer_DD is a Customer_DataDictionary
    End_Object    // Customer_DD

    Object SalesP_DD is a Salesp_DataDictionary
    End_Object    // SalesP_DD

    Object OrderHea_DD is a OrderHea_DataDictionary
        Set Constrain_file to Customer.File_number
        Set DDO_Server to Customer_DD
        Set DDO_Server to SalesP_DD
    End_Object    // OrderHea_DD

    Object OrderDtl_DD is a OrderDtl_DataDictionary
        Set Constrain_file to OrderHea.File_number
        Set DDO_Server to OrderHea_DD
        Set DDO_Server to Invt_DD
    End_Object    // OrderDtl_DD

    Set Main_DD to Customer_DD
    Set Server to Customer_DD

    
    Property Handle[] paRandoms
    
    Procedure CreateRandomGroups
        Integer i x y iCol iCount iFile
        Handle hFrm hGrp
        Handle[] aFrms
        
        Get Main_File of Customer_DD to iFile
        
        Get piColumnCount to iCount
        Move 0 to y
        
        For x from 0 to 50
            Get Create (RefClass(cWebGroup)) to hGrp
            
            Set Name of hGrp to ("oGeneratedGroup" + String(x))
            Set psCaption of hGrp to ("Group: " + String(x))
            Set piColumnCount of hGrp to iCount
            
        
        
            Move 0 to iCol
            For i from 0 to 11
                Get Create of hGrp (RefClass(cWebForm)) to hFrm
                
                Set Name of hFrm to ("OGENERATED" + String(y))
                Set psLabel of hFrm to ("Form " + String(y))
                Set psValue of hFrm to (String(y))
                Set piColumnIndex of hFrm to iCol
                
                Send Bind_Data of hFrm iFile (iCol + 2) 
                
                Increment iCol
                If (iCol >= iCount) Begin
                    Move 0 to iCol
                End
                
                Move hFrm to aFrms[y]
                Increment y
            Loop
        
        Loop
        
        Set paRandoms to aFrms
    End_Procedure
    
    Procedure CreateRandomTabs
        Integer i x y z iCol iCount iFile
        Handle hFrm hTabCon hTab
        Handle[] aFrms
        
        Get Main_File of Customer_DD to iFile
        
        Get piColumnCount to iCount
        Move 0 to y
        
        For x from 0 to 20
            Get Create (RefClass(cWebTabContainer)) to hTabCon
            
            Set Name of hTabCon to ("oGeneratedTabContainer" + String(x))
            
            
            For z from 0 to 5
                Get Create of hTabCon (RefClass(cWebTabPage)) to hTab
                Set Name of hTab to ("oTab" + String(z))
                Set psCaption of hTab to ("Tab " + String(z))
                Set piColumnCount of hTab to iCount
                
                Move 0 to iCol
                For i from 0 to 6
                    Get Create of hTab (RefClass(cWebForm)) to hFrm
                    
                    Set Name of hFrm to ("OGENERATED" + String(y))
                    Set psLabel of hFrm to ("Form " + String(y))
                    Set psValue of hFrm to (String(y))
                    Set piColumnIndex of hFrm to iCol
                    
                    Send Bind_Data of hFrm iFile (iCol + 2) 
                    
                    Increment iCol
                    If (iCol >= iCount) Begin
                        Move 0 to iCol
                    End
                    
                    Move hFrm to aFrms[y]
                    Increment y
                Loop
            Loop
        
            
        
        Loop
        
        Set paRandoms to aFrms
    End_Procedure
    
    Object oFirstOrder_btn is a cWebButton
        Set psCaption to 'First Order'
        Set pbServerOnClick to True
    
        // fires when the button is clicked
        Procedure OnClick
            Handle hoDDO
            Get Main_DD to hoDDO
            Clear DF_All
            Send Find to hoDDO GE 1            
        End_Procedure        
    End_Object
    
    Object oNextOrder_btn is a cWebButton
        Set psCaption to 'Next Order'
        Set pbServerOnClick to True
        Set piColumnIndex to 1
    
        // fires when the button is clicked
        Procedure OnClick
            Handle hoDDO
            Get Main_DD to hoDDO
            Send Find to hoDDO GT 1            
        End_Procedure       
    End_Object
    
    Object oChangeProps is a cWebButton
        Set psCaption to 'Change props'
        Set pbServerOnClick to True
        Set piColumnIndex to 2
    
        // fires when the button is clicked
        Procedure OnClick
            Handle[] aObj
            Integer i iTo
            
            Get paRandoms to aObj
            Move (SizeOfArray(aObj) - 1) to iTo
            
            For i from 0 to iTo
                WebSet psTextColor of aObj[i] to "blue"
                WebSet peLabelAlign of aObj[i] to alignRight
            Loop                    
        End_Procedure      
    End_Object
    
    Object oLoadSyncProps is a cWebForm
        Set psLabel to "Load Sync Props:"
    End_Object
    
    Object oSetSyncProps is a cWebForm
        Set psLabel to "Set Sync Props:"
        Set piColumnIndex to 1
    End_Object
    
    Object oGetChangedProps is a cWebForm
        Set psLabel to "Get Changed Props:"
        Set piColumnIndex to 2
    End_Object
    
    Object oTotalTime is a cWebForm
        Set psLabel to "Total Time:"
    End_Object
   
    Send CreateRandomGroups
    
End_Object


