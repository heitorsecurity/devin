Use cWebView.pkg
Use cWebForm.pkg
Use cWebEdit.pkg
Use cWebCombo.pkg
Use cWebButton.pkg
Use cWebTabContainer.pkg
Use cWebTabPage.pkg
Use cWebPanel.pkg
Use cWebCheckbox.pkg

Use Customer.DD
Use SalesP.dd
Use OrderHea.dd
Use cWebRadio.pkg
Use cWebLabel.pkg

Object oDemoCards is a cWebView

    Object Customer_DD is a Customer_DataDictionary
    End_Object    // Customer_DD
    Set Main_DD to Customer_DD
    Set Server to Customer_DD
    
    Set piColumnCount to 3
    Set psCaption to "Cards"
    
    Set psCSSClass to "CustomerView"
    Set piWidth to 800
    Set Verify_Save_Msg to 0   
    
    Object oCustomer_Number is a cWebForm
        Set psLabel to "Customer Num:" 
        Entry_Item Customer.Customer_Number
    End_Object
    
    Object oCustomer_Name is a cWebForm
        Set psLabel to "Name:"
        Set piColumnIndex to 1
        Set piColumnSpan to 2
        Entry_Item Customer.Name
    End_Object    // oCustomer_Name
    
    Object oCustomer_Status is a cWebCheckbox
        Set psLabel to "Active: "
        Set pbShowLabel to True
        Entry_Item Customer.Status
    End_Object
    
    Object oCardLayout is a cWebCardContainer
        Set piColumnSpan to 3
        
        Set pbServerOnCardChange to True
        // This shows how to use OnCardChange. Update the current tab each time there is a change
        // and set the the next and previous buttons' enabled state as needed.
        Procedure OnCardChange String sNewSelectedTab String sPrevSelectedTab
            Handle hoCard
            String sCaption
            
            Get CurrentCard to hoCard
            
            If (hoCard > 0) Begin
                WebGet psCaption of hoCard to sCaption
                
                WebSet psCaption of oCurrentTabLbl to ("Current tab: " + sCaption)                
                
                WebSet pbEnabled of oNextBtn to (hoCard <> oCommentsCard)
                WebSet pbEnabled of oPreviousBtn to (hoCard <> oAddressCard)
            End
            Else Begin
                WebSet psCaption of oCurrentTabLbl to ("Current tab: unknown")
            End
            
            
        End_Procedure
        
        Object oAddressCard is a cWebCard
            Set psCaption to "Address"
            Set piColumnCount to 3
            
            Object oCustomer_Address is a cWebForm
                Entry_Item Customer.Address
                Set psLabel to "Street Address:"
                Set piColumnSpan to 2
            End_Object    // oCustomer_Address
        
            Object oCustomer_City is a cWebForm
                Entry_Item Customer.City
                Set psLabel to "City:"
            End_Object    // oCustomer_City
        
            Object oCustomer_State is a cWebCombo
                Entry_Item Customer.State
                Set psLabel to "State:"
                Set piColumnIndex to 1   // 1
                Set peLabelAlign to 1
            End_Object    // oCustomer_State
        
            Object oCustomer_Zip is a cWebForm
                Entry_Item Customer.Zip
                Set psLabel to "Zip / Postal Code:"
                Set piColumnIndex to 2
            End_Object    // oCustomer_Zip
        
            Object oCustomer_Phone_number is a cWebForm
                Entry_Item Customer.Phone_Number
                Set psLabel to "Phone Number:"
                Set piColumnIndex to 0
            End_Object    // oCustomer_Phone_number
        
            Object oCustomer_Fax_number is a cWebForm
                Entry_Item Customer.Fax_Number
                Set psLabel to "Fax Number:"
                Set piColumnIndex to 1
            End_Object    // oCustomer_Fax_number
        
            Object oCustomer_Email_address is a cWebForm
                Entry_Item Customer.EMail_Address
                Set psLabel to "E-Mail Address:"
                Set piColumnIndex to 2
            End_Object    // oCustomer_Email_address
            
        End_Object
        
        Object oBalancesCard is a cWebCard
            Set psCaption to "Balances"
            Set piColumnCount to 3
            
            Object oCustomer_Credit_Limit is a cWebForm
                Entry_Item Customer.Credit_limit
                Set psLabel to "Credit Limit:"
                Set piColumnIndex to 0
            End_Object    // oCustomer_Credit_Limit
        
            Object oCustomer_Purchases is a cWebForm
                Entry_Item Customer.Purchases
                Set psLabel to "Total Purchases:"
            End_Object    // oCustomer_Purchases
        
            Object oCustomer_Balance is a cWebForm
                Entry_Item Customer.Balance
                Set psLabel to "Balance Due:"
            End_Object    // oCustomer_Balance
            
        End_Object
        
        
        Object oCommentsCard is a cWebCard
            Set psCaption to "Comments"
            
            Object oCustomer_Comments is a cWebEdit
                Entry_Item Customer.Comments
                Set psLabel to "Comments:"
                Set psCSSClass to "BigBlueField"
                Set pbFillHeight to True
            End_Object    // oCustomer_Comments


        End_Object     

    End_Object

    
    Object oAddressBtn is a cWebButton
        Set psCaption to 'Address Card'
    
        Procedure OnClick
            Send Show of oAddressCard
        End_Procedure        
    End_Object
    
    Object oBalancesBtn is a cWebButton
        Set psCaption to 'Balances Card'
        Set piColumnIndex to 1
    
        Procedure OnClick
            Send Show of oBalancesCard
        End_Procedure
        
    End_Object
    
    Object oCommentsBtn is a cWebButton
        Set psCaption to 'Comments Card'
        Set piColumnIndex to 2
    
        Procedure OnClick
            Send Show of oCommentsCard        
        End_Procedure        
    End_Object  
    
    
    Object oPreviousBtn is a cWebButton
        Set psCaption to 'Previous Card'
        Set pbEnabled to False
    
        Procedure OnClick
            Send PreviousCard of oCardLayout
        End_Procedure      
    End_Object


    Object oNextBtn is a cWebButton
        Set psCaption to 'Next Card'
        Set piColumnIndex to 2
    
        Procedure OnClick
            Send NextCard of oCardLayout
        End_Procedure      
    End_Object

        
    Object oBorderChk is a cWebCheckbox
        Set psCaption to "Card Border"
        
        Set pbServerOnChange to True
        Procedure OnChange String sNewValue String sOldValue
            Boolean bChecked
            
            Get GetChecked to bChecked
            
            WebSet pbShowBorder of oCardLayout to bChecked
        End_Procedure
    End_Object
    
    
    Object oLabelChk is a cWebCheckbox
        Set psCaption to "Card Captions"
        
        Set pbServerOnChange to True
        Procedure OnChange String sNewValue String sOldValue
            Boolean bChecked
            
            Get GetChecked to bChecked
            
            WebSet pbShowCaption of oCardLayout to bChecked
        End_Procedure
    End_Object
    
    Object oCurrentTabLbl is a cWebLabel
        Set psCaption to "Current card: Address"
    End_Object


    
End_Object // oCustomer