Use cWebView.pkg
Use cWebPanel.pkg
Use cWebForm.pkg 
Use cWebTabContainer.pkg
Use cWebTabPage.pkg
Use cWebEdit.pkg
Use cWebCheckbox.pkg
Use cWebGroup.pkg

Use Vendor.DD
Use Customer.DD
Use Invt.DD

Object oDemoMultiDDOStructure is a cWebView
    Set piWidth to 900
    Set psCaption to "Multi DDO Structure"
    Set piColumnCount to 10
    
    // DDO Structure 1: this will be the view's main DDO structure
    Object oCustomer_DD is a Customer_DataDictionary
    End_Object

    Set Main_DD to oCustomer_DD
    
    // DDO Structure 2: We need to register this so it will be synchronized at the client
    Object oInactiveCustomer_DD is a Customer_DataDictionary
        Procedure OnConstrain
            Constrain Customer.Status eq "N"
        End_Procedure
    End_Object
    
    Send AddDDOStructure oInactiveCustomer_DD
    
    // DDO Structure 3: We need to register this so it will be synchronized at the client
    Object oVendor_DD is a Vendor_DataDictionary
    End_Object
    
    Send AddDDOStructure oVendor_DD

    // DDO Structure 4: We need to register this so it will be synchronized at the client
    Object oVendorTwo_DD is a Vendor_DataDictionary
    End_Object

    Object oInvt_DD is a Invt_DataDictionary
        Set DDO_Server to oVendorTwo_DD
    End_Object

    Send AddDDOStructure oInvt_DD

    Object oCustomerContainer is a cWebGroup
        Set psCaption to "All Customers:"
        Set piColumnCount to 9
        Set piColumnSpan to 5
        
        Set Server to oCustomer_DD

        Object oCustomer_Customer_Number is a cWebForm
            Entry_Item Customer.Customer_Number
            Set piColumnSpan to 0
            Set psLabel to "Customer Number:"
            Set peAlign to alignLeft
        End_Object

        Object oCustomer_Name is a cWebForm
            Entry_Item Customer.Name
            Set piColumnSpan to 0
            Set psLabel to "Customer Name:"
        End_Object

        Object oCustomer_Address is a cWebForm
            Entry_Item Customer.Address
            Set piColumnSpan to 0
            Set psLabel to "Street Address:"
        End_Object

        Object oCustomer_City is a cWebForm
            Entry_Item Customer.City
            Set piColumnSpan to 0
            Set psLabel to "City:"
        End_Object

        Object oCustomer_Comments is a cWebEdit
            Entry_Item Customer.Comments
            Set piColumnSpan to 0
            Set pbShowLabel to False
        End_Object           
    End_Object

    Object oInactiveCustomerContainer is a cWebGroup
        Set psCaption to "Inactive Customers:"
        Set piColumnIndex to 5
        Set piColumnCount to 9
        
        Set Server to oInactiveCustomer_DD

        Object oCustomer_Customer_Number is a cWebForm
            Entry_Item Customer.Customer_Number
            Set piColumnSpan to 0
            Set psLabel to "Customer Number:"
            Set peAlign to alignLeft
        End_Object

        Object oCustomer_Name is a cWebForm
            Entry_Item Customer.Name
            Set piColumnSpan to 0
            Set psLabel to "Customer Name:"
        End_Object

        Object oCustomer_Address is a cWebForm
            Entry_Item Customer.Address
            Set piColumnSpan to 0
            Set psLabel to "Street Address:"
        End_Object

        Object oCustomer_City is a cWebForm
            Entry_Item Customer.City
            Set piColumnSpan to 0
            Set psLabel to "City:"
        End_Object

        Object oCustomer_Comments is a cWebEdit
            Entry_Item Customer.Comments
            Set piColumnSpan to 0
            Set pbShowLabel to False
        End_Object           

        Object oCustomer_Status is a cWebCheckBox
            Entry_Item Customer.Status
            Set piColumnSpan to 0
            Set psCaption to "Active Status"
            Set pbShowLabel to True
        End_Object
    End_Object
    
    Object oVendorContainer is a cWebGroup
        Set psCaption to "Vendor:"
        Set piColumnSpan to 5
        Set piColumnCount to 9
            
        Set Server to oVendor_DD

        Object oVendor_ID is a cWebForm
            Entry_Item Vendor.ID
            Set piColumnSpan to 0
            Set psLabel to "Vendor ID:"
            Set peAlign to alignLeft
        End_Object

        Object oVendor_Name is a cWebForm
            Entry_Item Vendor.Name
            Set piColumnSpan to 0
            Set psLabel to "Vendor Name:"
        End_Object

        Object oVendor_Address is a cWebForm
            Entry_Item Vendor.Address
            Set piColumnSpan to 0
            Set psLabel to "Street Address:"
        End_Object

        Object oVendor_City is a cWebForm
            Entry_Item Vendor.City
            Set piColumnSpan to 0
            Set psLabel to "City:"
        End_Object            
    End_Object
    
    Object oInventoryContainer is a cWebGroup
        Set psCaption to "Inventory:"
        Set piColumnIndex to 5
        Set piColumnCount to 9
        
        Set Server to oInvt_DD

        Object oInvt_Item_ID is a cWebForm
            Entry_Item Invt.Item_ID
            Set piColumnSpan to 0
            Set psLabel to "Inventory Item:"
            Set peAlign to alignLeft
        End_Object

        Object oInvt_Description is a cWebForm
            Entry_Item Invt.Description
            Set piColumnSpan to 0
            Set psLabel to "Description:"
        End_Object

        Object oVendor_Name1 is a cWebForm
            Entry_Item Vendor.Name
            Set piColumnSpan to 0
            Set psLabel to "Vendor:"
        End_Object

        Object oInvt_Unit_Price is a cWebForm
            Entry_Item Invt.Unit_Price
            Set piColumnSpan to 0
            Set psLabel to "Unit Price:"
            Set peAlign to alignLeft
        End_Object

        Object oInvt_On_Hand is a cWebForm
            Entry_Item Invt.On_Hand
            Set piColumnSpan to 0
            Set psLabel to "On Hand:"
            Set peAlign to alignLeft
        End_Object
        
    End_Object
End_Object
