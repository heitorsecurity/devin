//
//  This shows the basic structure of a modal dialog. It explains where the controls go and how to 
//  initialize it (PopupTheDialog & OnShow). The DialogResult method explains how to return data 
//  from your dialog. The commented oOpenDialog_btn is an example of a button that opens this dialog. 
//  This button would be placed on another view.
//
//    Object oOpenDialog_btn is a cWebButton
//        Set psCaption to "Open Dialog"
//        
//        Procedure OnClick
//            Send PopupTheDialog of oWebModalDialog Self "Param 1" "Param 2"
//        End_Procedure
//        
//        Procedure OnCloseModalDialog Handle hoModalDialog
//            String sResult
//            
//            Get DialogResult of hoModalDialog to sResult
//        End_Procedure
//    End_Object
//


Use cWebModalDialog
Use cWebPanel.pkg
Use cWebButton.pkg
Use cWebForm.pkg
Use cWebTabContainer.pkg
Use cWebTabPage.pkg
Use Customer.DD
Use cWebCombo.pkg
Use cWebEdit.pkg

Object oDemoCustomerDialog is a cWebModalDialog
    Object oCustomer_DD is a Customer_DataDictionary
    End_Object

    Set Main_DD to oCustomer_DD
    Set Server to oCustomer_DD

    Set psCaption to "Edit Customer"
    Set piMinWidth to 600
    Set piMinHeight to 350
    Set pbServerOnSubmit to True    // enable the OnSubmit event
    
    Object oMainPanel is a cWebPanel
        Set piColumnCount to 12

        Object oCustomer_Customer_Number is a cWebForm
            Entry_Item Customer.Customer_Number
            Set piColumnSpan to 5
            Set psLabel to "Number:"
            Set piLabelOffset to 80
        End_Object

        Object oCustomer_Name is a cWebForm
            Entry_Item Customer.Name
            Set piColumnSpan to 0
            Set psLabel to "Name:"
            Set piColumnIndex to 5
            Set piLabelOffset to 80
        End_Object

        Object oTabContainer is a cWebTabContainer
            Set pbFillHeight to True
            Object oAddressTab is a cWebTabPage
                Set psCaption to "Address"
                Set piColumnCount to 12

                Object oCustomer_Address is a cWebForm
                    Entry_Item Customer.Address
                    Set piColumnSpan to 12
                    Set psLabel to "Street Address:"
                End_Object

                Object oCustomer_City is a cWebForm
                    Entry_Item Customer.City
                    Set piColumnSpan to 6
                    Set psLabel to "City / State / Zip:"
                End_Object

                Object oCustomer_State is a cWebCombo
                    Entry_Item Customer.State
                    Set piColumnSpan to 3
                    Set psLabel to "State:"
                    Set piColumnIndex to 6
                    Set pbShowLabel to False
                End_Object

                Object oCustomer_Zip is a cWebForm
                    Entry_Item Customer.Zip
                    Set piColumnSpan to 3
                    Set psLabel to "Zip/Postal Code:"
                    Set pbShowLabel to False
                    Set piColumnIndex to 9
                End_Object

                Object oCustomer_Phone_Number is a cWebForm
                    Entry_Item Customer.Phone_Number
                    Set piColumnSpan to 6
                    Set psLabel to "Phone Number:"
                End_Object

                Object oCustomer_Fax_Number is a cWebForm
                    Entry_Item Customer.Fax_Number
                    Set piColumnSpan to 6
                    Set psLabel to "Fax Number:"
                    Set piColumnIndex to 6
                End_Object

                Object oCustomer_EMail_Address is a cWebForm
                    Entry_Item Customer.EMail_Address
                    Set piColumnSpan to 8
                    Set psLabel to "E-Mail Address:"
                End_Object
                
            End_Object

            Object oBalanceTab is a cWebTabPage
                Set psCaption to "Balance"
                Set piColumnCount to 12

                Object oCustomer_Credit_Limit is a cWebForm
                    Entry_Item Customer.Credit_Limit
                    Set piColumnSpan to 7
                    Set psLabel to "Credit Limit:"
                End_Object

                Object oCustomer_Purchases is a cWebForm
                    Entry_Item Customer.Purchases
                    Set piColumnSpan to 7
                    Set psLabel to "Total Purchases:"
                End_Object

                Object oCustomer_Balance is a cWebForm
                    Entry_Item Customer.Balance
                    Set piColumnSpan to 7
                    Set psLabel to "Balance Due:"
                End_Object
                
            End_Object

            Object oCommentsTab is a cWebTabPage
                Set psCaption to "Comments"
                Set piColumnCount to 12

                Object oCustomer_Comments is a cWebEdit
                    Entry_Item Customer.Comments
                    Set piColumnSpan to 0
                    Set psLabel to "Comments:"
                    Set pbFillHeight to True
                    Set pbShowLabel to False
                End_Object
                
            End_Object
        End_Object
        
        
    End_Object 
    
    Object oBottomPanel is a cWebPanel
        Set piColumnCount to 6
        Set peRegion to prBottom

        Object oOkButton is a cWebButton
            Set psCaption to C_$OK
            Set piColumnSpan to 1
            Set piColumnIndex to 4

            Procedure OnClick
                Send Ok
            End_Procedure
        End_Object 

        Object oCancelButton is a cWebButton
            Set psCaption to C_$Cancel
            Set piColumnSpan to 1
            Set piColumnIndex to 5

            Procedure OnClick
                Send Cancel
            End_Procedure
        End_Object 
    End_Object 

    Procedure OnSubmit
        Send Ok
    End_Procedure
    
    Procedure Ok
        Boolean bErr
        
        //  Do some calculations / saves / validations here ..
        Get Request_Validate of oCustomer_DD to bErr
        If (not(bErr)) Begin
            Send Request_Save of oCustomer_DD
            
            //  Note that we only forward send if we saved succesfully
            Forward Send Ok
        End
    End_Procedure

    Procedure Cancel
        Forward Send Cancel
    End_Procedure
    
    
    //  PopupCustomerDialog:
    //
    //  Custom popup method that stores the customer RowId in a web property before calling Show.
    Procedure PopupCustomerDialog  Handle hReturnObj String sCustomerRowID
        // Initialize any object properites as needed via WebSet
        WebSet psCustomerRowID to sCustomerRowID
        
        // Invoke the modal dialog    
        Send Popup hReturnObj
    End_Procedure
    
    { WebProperty=True }
    Property String psCustomerRowID ""  //  Used to store the customer RowId between PopupCustomerDialog and OnShow
    
    Set pbServerOnShow to True
    
    
    //  OnShow:
    //
    //  Called by the Framework each time the dialog is displayed. We need to find the right customer here.
    Procedure OnShow
        String sRowID
        RowID rRowID
        
        //  Find your records here
        WebGet psCustomerRowID to sRowID
        
        Move (DeserializeRowID(sRowID)) to rRowID
        If (not(IsNullRowID(rRowID))) Begin
            Send FindByRowId of oCustomer_DD Customer.File_Number rRowID
        End
        Else Begin
            Send Clear of oCustomer_DD
        End
        
        WebSet psCustomerRowID to ""
    End_Procedure
    
    

End_Object