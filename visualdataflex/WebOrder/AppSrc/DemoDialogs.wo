Use cWebView.pkg
Use cWebButton.pkg
Use cWebApp.pkg
Use cWebHorizontalLine.pkg

Use DemoActionDialog.wo
Use DemoQuestionDialog.wo
Use DemoFileDialog.wo

Object oDemoDialogs is a cWebView
    Set piWidth to 300
    Set psCaption to "Dialogs"
    
    Object oInfoBtn is a cWebButton
        Set psCaption to "Standard Info Dialog"
        
        Procedure OnClick
            Send ShowInfoBox of oWebApp "Hello world!"
        End_Procedure
    End_Object
    
    Object oConfirmBtn is a cWebButton
        Set psCaption to "Standard Confirmation Dialog"
        
        Procedure ConfirmResponse Integer eConfirmMode
            If (eConfirmMode = cmYes) Begin
                WebSet pbEnabled to False
            End
        End_Procedure
        //  Publish the response method (Security!)
        WebPublishProcedure ConfirmResponse
        
        Procedure OnClick
            Send ShowYesNo of oWebApp (Self) (RefProc(ConfirmResponse)) "Are you sure you want to disable this button?" "Question title!"
        End_Procedure
    End_Object
    
    Object oCustomMessage is a cWebButton
        Set psCaption to "Custom Message Box"
        
        Procedure CustomResponse Integer eBtn
            Send ShowInfoBox ("You liked " + String(eBtn) + "!")
        End_Procedure
        //  Publish the response method (Security!)
        WebPublishProcedure CustomResponse

        Procedure OnClick
            String[] aButtons
            
            Move "One"      to aButtons[0]
            Move "Two"      to aButtons[1]
            Move "Three"    to aButtons[2]
            Move "Four"     to aButtons[3]
            
            Send ShowMessageBox of oWebApp Self (RefProc(CustomResponse)) "Pick your favorite number!" "Multiple choice" "WebMsgBoxConfirm" 1  aButtons
        End_Procedure        
    End_Object

    Object oWebHorizontalLine1 is a cWebHorizontalLine
    End_Object

    Object oStandardAction is a cWebButton
        Set psCaption to "Standard Question Dialog"
        
        // Standard dialog calls OnCloseModalDialog callback upon completion
        Procedure OnCloseModalDialog Handle hoModalDialog
            Boolean bCancel
            String sAnswer
            Send GetAnswers of hoModalDialog (&sAnswer) (&bCancel) 
            If (bCancel) Begin
                Send ShowInfoBox ("You didn't want to answer, did you?")
            End
            Else Begin
                Send ShowInfoBox ("Your name is '" + sAnswer + "'!!!")
            End
        End_Procedure

        Procedure OnClick
            Send InitializeDialog of oDemoQuestionDialog "Question" "What is your name?" 
            Send Popup of oDemoQuestionDialog Self
        End_Procedure
        
    End_Object
    
    Object oCustomAction is a cWebButton
        Set psCaption to "Custom Question Dialog"
        
        Procedure HandleResponse Boolean bOk String sAnswer
            If (bOk) Begin
                Send ShowInfoBox ("Your name is '" + sAnswer + "'!!!")
            End
            Else Begin
                Send ShowInfoBox ("You didn't want to answer, did you?")
            End
        End_Procedure
        //  Publish the response method (Security!)
        WebPublishProcedure HandleResponse

        Procedure OnClick
            Send AskQuestion of oDemoActionDialog "Question" "What is your name?" (Self) (RefProc(HandleResponse))
        End_Procedure
    End_Object
    
    Object oWebHorizontalLine2 is a cWebHorizontalLine
    End_Object
    
    Object oErrorDialog is a cWebButton
        Set psCaption to "Standard Error Dialog"
        
        Procedure OnClick
            Error 150 "Demo error message shown as unhandled error!"
        End_Procedure
    End_Object
    
    Object oUserErrorDialog is a cWebButton
        Set psCaption to "User Error Dialog"
        
        Procedure OnClick
            Send UserError "Friendly error message using the UserError procedure." "Friendly error message :-)"
        End_Procedure
    End_Object

    Object oUnhandledClientError is a cWebButton
        Set piColumnSpan to 0
        Set psCaption to "Clientside Error Dialog"
    
        Set psClientOnClick to "DoesNotExist"
        Set pbServerOnClick to False
    End_Object

    Object oWebHorizontalLine3 is a cWebHorizontalLine
    End_Object
    
    
    Object oOpenFile is a cWebButton
        Set psCaption to "File Dialog"
        
        Procedure OnClick
            Send Show of oDemoFileDialog
        End_Procedure
    End_Object
End_Object


