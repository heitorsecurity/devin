Use cWebModalDialog.pkg
Use cWebButton.pkg
Use cWebLabel.pkg
Use cWebPanel.pkg
Use cWebForm.pkg

Object oDemoActionDialog is a cWebModalDialog
    Set psCaption to "Question.."
    Set pbResizable to False
    Set piWidth to 400
    Set piColumnCount to 4
    
    { WebProperty=True }
    Property String psReturnMsg ""
    
    { WebProperty=True }
    Property String psReturnObj ""
    
    Object oMainPanel is a cWebPanel
        Set peRegion to prCenter
        
        Object oQuestionLbl is a cWebLabel
            Set psCaption to "Are you sure that you want to do this?"
        End_Object
        
        Object oResponseFrm is a cWebForm
            Set pbShowLabel to False
        End_Object
    End_Object
    
    Object oButtonPanel is a cWebPanel
        Set peRegion to prBottom
        Set piColumnCount to 4
        
        Object oOkBtn is a cWebButton
            Set pbShowLabel to False
            Set psCaption to "Ok"
            Set piColumnIndex to 1
            
            Procedure OnClick
                Send SendResponse True
            End_Procedure
        End_Object
        
        Object oCancelBtn is a cWebButton
            Set pbShowLabel to False
            Set psCaption to "Cancel"
            Set piColumnIndex to 2
            
            Procedure OnClick
                Send SendResponse False
            End_Procedure
        End_Object
    End_Object
    
    Set pbServerOnSubmit to True
    Procedure OnSubmit
        Send SendResponse True
    End_Procedure
    
    Procedure AskQuestion String sTitle String sQuestion Handle hReturnObj Handle hReturnMsg
        String sWO sMethod
        
        Get WebObjectName of hReturnObj to sWO
        Get WebMethodName of hReturnObj hReturnMsg to sMethod
        If (sMethod <> "") Begin
            WebSet psReturnObj to sWO
            WebSet psReturnMsg to sMethod
            
            WebSet psCaption of oDemoActionDialog to sTitle
            WebSet psCaption of oQuestionLbl to sQuestion
            
            WebSet psValue of oResponseFrm to ""
                    
            Send Show of oDemoActionDialog
        End
    End_Procedure
    
    Procedure SendResponse Boolean bOk
        Handle hReturnMsg hReturnObj
        String sName sResponse
        
        //  Get handle to web object
        WebGet psReturnObj of oDemoActionDialog to sName
        Get WebObjectByName sName to hReturnObj
        
        If (hReturnObj > 0) Begin
            //  Get handle to return message
            WebGet psReturnMsg of oDemoActionDialog to sName
            Get WebMethodByName of hReturnObj sName to hReturnMsg
                
            If (hReturnMsg > 0) Begin
                WebGet psValue of oResponseFrm to sResponse
                Send Hide of oDemoActionDialog

                //  Call handler method
                Send hReturnMsg of hReturnObj bOk sResponse
            End
        End
    End_Procedure
End_Object



