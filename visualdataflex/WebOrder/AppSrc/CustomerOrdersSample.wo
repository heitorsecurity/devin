Use cWebView.pkg
Use cWebPanel.pkg
Use cWebForm.pkg
Use cWebRadio.pkg
Use cWebCheckbox.pkg
Use cWebForm.pkg
Use cWebButton.pkg
Use cWebLabel.pkg
Use cWebIFrame.pkg
Use cWebSpacer.pkg
Use cWebGroup.pkg

// Note: This sample does not actually run a DataFlex Reports report. The .pdf file that would normally
// be generated by DataFlex Reports is a previously generated file, which was installed with DataFlex.

// If you have an installed version of DataFlex Reports compatible with this version of DataFlex, then
// you could comment out the code below to generate a live DataFlex Reports report.
Object oCustomerOrdersSample is a cWebView
    Set psCaption to "Customer Orders (DataFlex Reports)"
    Set piMinWidth to 800
    Set piColumnCount to 10

// If you are using DataFlex Reports, here is how you would actually
// initialize, run and save the report to a PDF file.
//
//    Use cDRReport.pkg // Move this line to the other USE statement lines above the cWebView component
//
//    Object oReport is a cDRReport
//        Procedure OnInitializeReport
//            Send SetFilters
//        End_Procedure
//
//        Procedure SetFilters
//            Integer[] iCustomerCustomerNumbers
//            String sReportId
//
//            Get psReportId to sReportId
//            Send RemoveAllFilters sReportId  // clear any previous filters
//
//            WebGet psValue of oFromCustomerCustomerNumber to iCustomerCustomerNumbers[0]
//            WebGet psValue of oToCustomerCustomerNumber to iCustomerCustomerNumbers[1]
//
//            Move (SortArray (iCustomerCustomerNumbers)) to iCustomerCustomerNumbers
//            If (iCustomerCustomerNumbers[0] <> 0) Begin
//                Send AddFilter C_USEMAINDRREPORTID "{Customer.Customer_Number}" C_DRGreaterThanOrEqual iCustomerCustomerNumbers[0]
//            End
//            If (iCustomerCustomerNumbers[1] <> 0) Begin
//                Send AddFilter C_USEMAINDRREPORTID "{Customer.Customer_Number}" C_DRLessThanOrEqual iCustomerCustomerNumbers[1]
//            End
//        End_Procedure
//
//        Function GenerateReport Returns String
//            String sReportId sPath
//            DRPDFExportOptions PDFReportOptions
//            Boolean bCanceled
//
//            Set psReportName to "CustomerOrders.DR"
//            Get OpenReport to sReportId
//
//            If (sReportId <> "") Begin
//                Get DefaultPDFExportOptions to PDFReportOptions
//                Move C_drHigh to PDFReportOptions.iImageQuality
//                Set pPDFExportOptions to PDFReportOptions
//
//                Get ReportCacheFileName '.pdf' to sPath
//                If (sPath <> "") Begin
//                    Send ExportReport C_DRPDF sPath
//
//                    // see if report ran ok, if not return empty path
//                    Get pbCanceled to bCanceled
//                    If (bCanceled) Begin
//                        Move "" to sPath
//                    End
//                End
//
//                Send CloseReport sReportId
//            End
//
//            Function_Return sPath
//        End_Function
//    End_Object

// This is a dummy object that is used in place of a report PDF that would have been
// generated using DataFlex Reports. Note that this also shows you how to download a
// static file (could be any kind of file) from a secure non-web location. In this case,
// the dummy report results is located in \ReportsCache folder, which is not a web path.

    Object oReport is a cObject
        Function GenerateReport Returns String
            String sPath

            Get GetReportsCache of ghoWebApp True to sPath
            If (sPath <> "") Begin
                Move (sPath + "\CustomerOrderSample.pdf") to sPath
            End
            Function_Return sPath
        End_Function
    End_Object

    Procedure RunReport
        String sFile sUrl

        // generate the report file and return its full file path name (not a URL)
        Get GenerateReport of oReport to sFile

        If (sFile="") Begin
            Send ShowInfoBox "The Report could not be generated"
        End
        Else Begin
            Get DownloadURL of ghoWebResourceManager sFile to sUrl

            // this is used to show a PDF file in a new browser tab or window
            // use btNewTab to open a new browser tab page
            // use btNewWindow to open a new browser window
            //Send NavigateToPage sUrl btNewTab

            // this is used to show PDF in an embedded viewer within this view
            WebSet psUrl of oWebIFrame to sUrl
        End
    End_Procedure

    Object oWebPanelTop is a cWebPanel
        Set peRegion to prTop

        Object oWebPanelTopLeft is a cWebPanel
            Set peRegion to prLeft
            Set piWidth to 200

            Object oCustomerCustomerNumberGroup is a cWebGroup
                Set psCaption to "Customer Number:"
                Set piColumnSpan to 0
                Set piColumnCount to 10

                Object oFromCustomerCustomerNumber is a cWebform
                   Set psLabel to "From:"
                   Set piLabelOffset to 50
                   Set piColumnSpan to 0
                End_Object

                Object oToCustomerCustomerNumber is a cWebform
                   Set psLabel to "To:"
                   Set piLabelOffset to 50
                   Set piColumnSpan to 0
                End_Object
            End_Object

            Object oRunButton is a cWebButton
                Set psCaption to "Run Report"
                Set piColumnSpan to 0

                Procedure OnClick
                    Send RunReport
                End_Procedure
            End_Object
        End_Object

        Object oWebPanelTopCenter is a cWebPanel
            Set peRegion to prCenter

            Object oWebLabel1 is a cWebLabel
                Set piColumnSpan to 0
                Set psCaption to "Note: This view simply displays a static file ('CustomerOrderSample.pdf'), so these settings do not really change the output."
                Set piColumnIndex to 3
            End_Object
        End_Object
    End_Object

    Object oWebPanelCenter is a cWebPanel
        Set peRegion to prCenter

        // output to an embedded window
        Object oWebIFrame is a cWebIFrame
           Set pbFillHeight to True
           Set pbShowBorder to True
        End_Object
    End_Object
End_Object
